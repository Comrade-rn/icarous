Boolean Lookup trafficConflict;
Boolean Lookup trafficSpeedConflict;
Boolean Lookup trafficAltConflict;
Boolean Lookup trafficTrackConflict;
Boolean Lookup nextWPFeasibility;
Boolean Lookup restartMission;
Boolean Lookup directPathToNextFeasibleWP;
Real Lookup preferredTrack;
Real Lookup preferredSpeed;
Real Lookup preferredAltitude;
Real Lookup interceptHeading;
Real[3] Lookup velocity;
Real[3] Lookup position;
Integer Lookup resolutionType;

Boolean    Command CheckSafeToTurn(Real fromHeading,Real toHeading);

Command SetVel(Real velocity[3]);
Command pprint(...);


TRAFFIC_RESOLUTION:{
    InOut Boolean completionStatus;
    In Integer nextWP;
    InOut Real resSpeed;
    Real acPosition[3];
    Real acVelocity[3];

    Real velCmd[3];
    Real prevSpeedResCmd[3];
    Real prevTrackResCmd[3];
    Real prevAltResCmd[3];
    Real returnVel;
    Real trackPref;
    Real speedPref;
    Real altPref;
    Boolean returnSafe = false;
    Boolean statusTF;
    Boolean statusTF_Speed;
    Boolean statusTF_Altitude;
    Boolean statusTF_Track;
    Boolean SpeedResolution = false;
    Boolean AltResolution = true;
    Boolean TrackResolution = false;
    Boolean dpNextWp = false;
    Boolean reset = false;
    Integer resType;

    acVelocity = Lookup(velocity);
    resType    = Lookup(resolutionType);

    prevSpeedResCmd[0] = -100000;
    prevSpeedResCmd[1] = -100000;
    prevSpeedResCmd[2] = -100000;

    prevTrackResCmd[0] = -100000;
    prevTrackResCmd[1] = -100000;
    prevTrackResCmd[2] = -100000;

    prevAltResCmd[0] = -100000;
    prevAltResCmd[1] = -100000;
    prevAltResCmd[2] = -100000;

    if(resType == 0){
        SpeedResolution = true;
        AltResolution = false;
        TrackResolution = false;
    }elseif(resType == 1){
        SpeedResolution = false;
        AltResolution = true;
        TrackResolution = false;
    }elseif(resType == 2){
        SpeedResolution = false;
        AltResolution = false;
        TrackResolution = true;
    }endif



    {
        // Get traffic conflict status
        Boolean executed = false;
        Repeat (!returnSafe || statusTF) && (!reset);

        statusTF = Lookup(trafficConflict) ;
        statusTF_Speed = Lookup(trafficSpeedConflict);
        statusTF_Altitude = Lookup(trafficAltConflict);
        statusTF_Track = Lookup(trafficTrackConflict);
        dpNextWp = Lookup(directPathToNextFeasibleWP);
        reset = Lookup(restartMission);

        acPosition = Lookup(position);

        //pprint("position:",acPosition[0],acPosition[1],acPosition[2]);
        //pprint("speed conflict:",statusTF_Speed);
        //pprint("altitude conflict:",statusTF_Altitude);
        //pprint("track conflict:",statusTF_Track);
        //pprint(reset);

        SPEED:{
           SkipCondition !statusTF_Speed || TrackResolution || AltResolution;

           speedPref = Lookup(preferredSpeed);

           //pprint("preferred speed:",speedPref);

           if(speedPref >= 0){
               velCmd[0] = acVelocity[0];
               velCmd[1] = speedPref;
               velCmd[2] = 0;
               //pprint("velocity command :",velCmd[0],velCmd[1],velCmd[2]);
               SetVel(velCmd);
               prevSpeedResCmd[0] = velCmd[0];
               prevSpeedResCmd[1] = velCmd[1];
               prevSpeedResCmd[2] = velCmd[2];
               SpeedResolution = true;
           }elseif(speedPref > -10000){
               SetVel(prevSpeedResCmd);
               SpeedResolution = true;
           }else{
               //SpeedResolution = false;
               //SetVel(prevSpeedResCmd);
               //pprint("no speed resolution");
           }endif

           returnSafe = dpNextWp;
        }

        ALT:{
           Real climbRate = 0;
           Real diff;
           SkipCondition !statusTF_Altitude || TrackResolution || SpeedResolution;

           altPref = Lookup(preferredAltitude);

           //pprint("preferred altitude:",altPref);

           diff = altPref - acPosition[2];

           //pprint("altitude diff:",diff);
           if(altPref > 0 && altPref < 10000){

              if(diff > 25){
                   diff = 25;
              }elseif(diff < -25){
                   diff = -25;
              }endif


              // The negative sign is require to convert the climb rate to a Vd value
              climbRate = -0.5* diff;

              //pprint("climb rate:",climbRate);

              velCmd[0] = acVelocity[0];
              velCmd[1] = acVelocity[1];
              velCmd[2] = climbRate;
              SetVel(velCmd);
              prevAltResCmd[0] = velCmd[0];
              prevAltResCmd[1] = velCmd[1];
              prevAltResCmd[2] = velCmd[2];
              //pprint("velocity command :",velCmd[0],velCmd[1],velCmd[2]);
              AltResolution = true;
           }elseif(altPref > -10000 && altPref < 0){
              //pprint("Maintaining prev cmd");
              prevAltResCmd[2] = 0;
              SetVel(prevAltResCmd);
              AltResolution = true;
           }else{
              //AltResolution = false;
           }endif

        }

        TRACK:{

            SkipCondition !statusTF_Track || SpeedResolution || AltResolution;

            // Preferred track angle
            trackPref = Lookup(preferredTrack);

            //pprint("preferred track:",trackPref);

            if(trackPref >= 0){
                velCmd[0] = trackPref;
                velCmd[1] = resSpeed;
                velCmd[2] = 0;
                SetVel(velCmd);
                prevTrackResCmd[0] = velCmd[0];
                prevTrackResCmd[1] = velCmd[1];
                prevTrackResCmd[2] = velCmd[2];
                TrackResolution = true;
                //pprint("Setting velocity:",velCmd);
            }elseif(trackPref > -10000){
                SetVel(prevTrackResCmd);
                TrackResolution = true;
                //pprint("Setting prev velocity:",velCmd);
            }else{
                
            }endif
        }

        if(TrackResolution){
            Boolean _returnsafe;
            returnVel = Lookup(interceptHeading);
            //pprint("Return heading to plan:",returnVel);
            {
                EndCondition isKnown(_returnsafe);
                _returnsafe = CheckSafeToTurn(velCmd[0],returnVel);
            }
            returnSafe = _returnsafe;

            if(!returnSafe){
                if(prevTrackResCmd[0] >= 0){
                    SetVel(prevTrackResCmd);
                    //pprint("Setting prev velocity in returnSafe:",velCmd);
                }endif
            }endif
        }endif

        if(AltResolution){
           returnSafe = dpNextWp;
           if(!returnSafe){
               SetVel(prevAltResCmd);
           }endif
        }endif

    }

    completionStatus = true;
}