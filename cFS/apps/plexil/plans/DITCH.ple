// -*- Mode: C++ -*-
// Ditching
Command SetPos(Real position[3]);
Command SetMode(String mode);
Command pprint(...);
Real[3]    Command GetMissionWP(Integer index);

Boolean Lookup requireDitchGuidance;
Boolean Lookup ditchingComplete;
Boolean Lookup resetDitching;
Boolean Lookup ditchingStatus;
Real    Lookup resolutionSpeed;
Real[3] Lookup ditchSite;
Real Lookup time;
Boolean Lookup restartMission;

LibraryAction GOTO_POSITION_S2D (In String SearchType,
                             In Boolean TrafficConflict,
                             In Real TargetPosition[3],
                             In Real resSpeed,
                             In Integer PrevConflictId,
                             InOut Boolean resolutionCompleted);



DITCH:
{

   Boolean reqGuidance;
   Boolean success = false;
   Boolean restartDitching = false;
   Boolean restartStatus = false;
   Real resSpeed;
   Real captureH;
   EndCondition success || restartStatus;

   pprint("Plexil status:In Ditching plan");

   SetMode("ACTIVE");

   resSpeed  = Lookup(resolutionSpeed);
   captureH = resSpeed * 1.5;

   CORE:{

       {
           Real ditchsite[3];
           Boolean validSite = false;
           Repeat !success && !restartStatus;

           {
               Real waypoint[3];
               Repeat !validSite;

               success = Lookup(ditchingComplete);
               restartStatus = Lookup(restartMission);
               restartDitching = Lookup(ditchingStatus);
               //pprint("success:",success);

               ditchsite = Lookup(ditchSite);
               
               if(ditchsite[2] > -100.0){
                   pprint("Plexil status: received valid ditch site ",ditchsite[0],ditchsite[1],ditchsite[2]);
                   validSite = true;
               }endif



           }

           //pprint("Plexil status:checking guidance requirements");

           reqGuidance = Lookup(requireDitchGuidance);

           pprint("Plexil status:guidance requested:",reqGuidance);

           ICAROUS_GUIDED:{
              Boolean complete = false;
              Boolean rescomplete;
              StartCondition reqGuidance && !success;
              SkipCondition !reqGuidance;

              pprint("Plexil status: Ditching with Icarous guidance");
              resSpeed  = Lookup(resolutionSpeed);
              LibraryCall GOTO_POSITION_S2D(SearchType = "ASTAR",
                                        TrafficConflict = false,TargetPosition = ditchsite,
                                        resSpeed = resSpeed,
                                        PrevConflictId = 0,
                                        resolutionCompleted = rescomplete);
              complete = true;
              pprint("Plexil status: finished ditching");
           }


           DIRECT:{
              Boolean complete = false;
              StartCondition !reqGuidance && !success;
              SkipCondition reqGuidance;

              EndCondition (complete);

              pprint("Plexil status: Proceeding direct to ditch site");

              SetPos(ditchsite);

              complete = true;
           }


       }
   }

}
